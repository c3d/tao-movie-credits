// ****************************************************************************
//  movie_credits2.xl                                             Tao project
// ****************************************************************************
// 
//   File Description:
// 
//    Implement various kinds of scrolling credits
// 
// 
// 
// 
// 
// 
// 
// 
// ****************************************************************************
// This software is licensed under the GNU General Public License v3.
// See file COPYING for details.
//  (C) 2011 Christophe de Dinechin <christophe@taodyne.com>
//  (C) 2011 Jérôme Forissier <jerome@taodyne.com>
//  (C) 2011 Catherine Burvelle <cathy@taodyne.com>
//  (C) 2011 Taodyne SAS
// ****************************************************************************

import Slides
import Animate


// ============================================================================
//
//   Configuration 
//
// ============================================================================

// Size of the frame texture we use to accelerate rendering
mc_width -> 1200
mc_height -> 2500
mc_size W:integer, H:integer -> mc_width := W; mc_height := H



// ============================================================================
// 
//   Star Wars crawl
// 
// ============================================================================

mc_star_wars_crawl Duration:real, Body ->
// ----------------------------------------------------------------------------
//    Create a Star-Wars style text crawl
// ----------------------------------------------------------------------------
    locally
        if page_time > Duration then
            rotatex 5 * fade_in(page_time - Duration, 20)

        mc_swc_background
        mc_swc_planet Duration, mc_swc_planet_texture
        mc_swc_text Duration, Body


mc_star_wars_crawl_ship Delay:real, Speed:real, Body ->
// ----------------------------------------------------------------------------
//   Show a ship
// ----------------------------------------------------------------------------
    locally
        if page_time >= Delay - 2 then
            scale x:real -> scale x, x, x

            light 0
            light_position -400, 0, 2000
            color "white"
            rotatex -5
            translate 400, 200, Speed * (Delay - page_time)
            scale fade_out(page_time - Delay, Delay)
            Body


// ============================================================================
//    Settings
// ============================================================================

// Select the name of the stars in the background
mc_swc_background_texture -> "movie_credits:Movie-Stars-Background.jpg"
mc_swc_background_texture N:text -> mc_swc_background_texture := N

// Select the texture for the planet
mc_swc_planet_texture -> "movie_credits:Planet-Map.jpg"
mc_swc_planet_texture N:text -> mc_swc_planet_texture := N

// Select the texture for the planet clouds
mc_swc_clouds_texture -> "movie_credits:Planet-Clouds.png"
mc_swc_clouds_texture N:text -> mc_swc_clouds_texture := N

// Select how long before we roll down to the planet
mc_swc_planet_delay -> 25.0
mc_swc_planet_delay D:real -> mc_swc_planet_delay := D



// ****************************************************************************
// 
//    End credits
// 
// ****************************************************************************

mc_credits Duration:real, Speed:real, Body ->
// ----------------------------------------------------------------------------
//    Final credits
// ----------------------------------------------------------------------------
    // Do not refresh because of external timer events
    no_refresh_on TimerEvent

    contents 0,
        color "white"
        frame_texture mc_width, mc_height,
            vertical_align 0.5
            align 0.5
            font "Baskerville", "Times", 36
            Body

        locally
            rectangle 0, Speed*page_time-window_height*0.55, mc_width,mc_height


mc_credits_skip H:real ->
// ----------------------------------------------------------------------------
//   Skip a given height
// ----------------------------------------------------------------------------
    translatey -H


mc_credit Role:text, Name:text ->
// ----------------------------------------------------------------------------
//   Display a single credit
// ----------------------------------------------------------------------------
    mcx -> mc_credits_width * 0.5
    mcw -> mc_credits_width * 0.95
    text_box -mcx, 0, mcw, mc_credits_height,
        align 1
        text Role
    text_box mcx, 0, mcw, mc_credits_height,
        align 0
        text Name
    mc_credits_skip mc_credits_height


mc_credits_section H:real, Body ->
// ----------------------------------------------------------------------------
//   Add a centered section
// ----------------------------------------------------------------------------
    text_box 0, -H/2, mc_credits_width, H,
        align 0.5
        vertical_align 0.5
        Body
    mc_credits_skip H



// ============================================================================
//   Settings
// ============================================================================

// Width and height of an item
mc_credits_height -> 50.0
mc_credits_height H:real -> mc_credits_height := H
mc_credits_width -> 600.0
mc_credits_width W:real -> mc_credits_width := W



// ****************************************************************************
// 
//   Internal helpers
// 
// ****************************************************************************


mc_swc_background ->
// ----------------------------------------------------------------------------
//   Show the stars in the background
// ----------------------------------------------------------------------------
    background 0, 
        // We map the stars background on a large sphere in the background
        color "white"
        texture mc_swc_background_texture
        translate 0, 0, -2000
        rotatey 0.03 * time + 90 

        // We invert the sphere because we are inside it
        scale 1, -1, 1 

        // Wrap the texture and repeat it 8 times in each direction
        texture_wrap true, true 
        texture_transform 
            scale 6, 6, 6
        sphere 7000 


mc_swc_planet Delay:real, Texture:text ->
// ----------------------------------------------------------------------------
//   Draw the planet with the given texture
// ----------------------------------------------------------------------------
    background -500,
        color "white"
        texture mc_swc_planet_texture
        texture_wrap true, true
        texture_transform
            scale 4, 4, 4
        translate 0,-2700-100 * expm1(Delay - page_time),0
        rotatex -60
        rotatez 27
        rotatey 0.03 * time
        sphere 0, 0, 0, 5000,5000,5000,100,100

        texture ""
        color 70%, 80%, 100%, 10%
        sphere 0, 0, 0, 5010,5010,5010,100,100

        texture mc_swc_clouds_texture
        color "white"
        texture_wrap true, true
        texture_transform
            scale 2, 1, 1
        sphere 0, 0, 0, 5020,5020,5020,100,100


mc_swc_text Duration:real, Body ->
// ----------------------------------------------------------------------------
//   Draw the Star Wars scrolling text
// ----------------------------------------------------------------------------
    contents 0,
        frame_texture mc_width, mc_height,
            vertical_align 0.0 
            align 0.5, 1.0
            font "Arial Narrow", 64, bold 
            paragraph_space 50, 50
            color 1, 0.8, 0.2
            text_box 0, 0, mc_width, mc_height, Body

        // We want a smooth text
        texture_min_filter "LINEAR_MIPMAP_LINEAR"
        locally
            translate 0, -window_height/2, 0
            rotatex -70
            translatey mc_height * (3 * page_time / Duration - 1.5)
            color "white", 0.9+0.9*expm1 (Duration-page_time)
            scale 1, 3, 1
            rectangle 0, 0, mc_width, mc_height


// Add this module's images/ directory to the image search path
add_search_path "movie_credits:", module_dir & "/images"
