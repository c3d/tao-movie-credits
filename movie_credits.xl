// ****************************************************************************
//  credits.xl                                                      Tao project
// ****************************************************************************
// 
//   File Description:
// 
//    Implement various kinds of scrolling credits
// 
// 
// 
// 
// 
// 
// 
// 
// ****************************************************************************
// This document is released under the GNU General Public License.
// See http://www.gnu.org/copyleft/gpl.html and Matthew 25:22 for details
//  (C) 2011 Christophe de Dinechin <christophe@taodyne.com>
//  (C) 2011 Jérôme Forissier <jerome@taodyne.com>
//  (C) 2011 Catherine Burvelle <cathy@taodyne.com>
//  (C) 2011 Taodyne SAS
// ****************************************************************************

module_description
    id "350F794B-F88D-433C-994E-1D223662025C"
    name "Movie Credits"
    description "A few classical movie credits"
    import_name "MovieCredits"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version 1.0

module_description "fr",
    name "Génériques de films"
    description "Quelques exemples de génériques de films classiques"


// ****************************************************************************
// 
//   Star Wars crawl
// 
// ****************************************************************************

mc_star_wars_crawl Duration:real, Body ->
// ----------------------------------------------------------------------------
//    Create a Star-Wars style text crawl
// ----------------------------------------------------------------------------
    if page_time > Duration then
        fade_in  Value:real, Dur:real  -> -expm1(-5 * Value / Dur)
        rotatex 5 * fade_in(page_time - Duration, 20)

    mc_swc_background
    mc_swc_planet Duration, mc_swc_planet_texture
    mc_swc_text Duration, Body


mc_star_wars_crawl_ship Delay:real, Speed:real, Body ->
// ----------------------------------------------------------------------------
//   Show a ship
// ----------------------------------------------------------------------------
    locally
        if page_time >= Delay - 2 then
            scale3 x:real -> scale x, x, x
            fade_out Value:real, Duration:real  -> 1+expm1(-5 * Value/Duration)

            light 0
            light_position -400, 0, 2000
            color "white"
            rotatex -5
            translate 400, 200, Speed * (Delay - page_time)
            scale3 fade_out(page_time - Delay, Delay)
            Body


// ============================================================================
//    Settings
// ============================================================================

// Select the name of the stars in the background
mc_swc_background_texture -> "image:Movie-Stars-Background.jpg"
mc_swc_background_texture N:text -> mc_swc_background_texture := N

// Select the texture for the planet
mc_swc_planet_texture -> "image:Planet-Map.jpg"
mc_swc_planet_texture N:text -> mc_swc_planet_texture := N

// Select the texture for the planet clouds
mc_swc_clouds_texture -> "image:Planet-Clouds.png"
mc_swc_clouds_texture N:text -> mc_swc_clouds_texture := N

// Select how long before we roll down to the planet
mc_swc_planet_delay -> 25.0
mc_swc_planet_delay D:real -> mc_swc_planet_delay := D



// ****************************************************************************
// 
//    End credits
// 
// ****************************************************************************

mc_credits Duration:real, Speed:real, Body ->
// ----------------------------------------------------------------------------
//    Final credits
// ----------------------------------------------------------------------------
    locally
        mc_credits_y := -window_height * 0.55 + Speed * page_time
        translatey mc_credits_y
        vertical_align 0.5
        align 0.5
        font "Baskerville", "Times", 36
        color "white"
        Body


mc_credits_skip H:real ->
// ----------------------------------------------------------------------------
//   Skip a given height
// ----------------------------------------------------------------------------
    translatey -H
    mc_credits_y := mc_credits_y - H


mc_credits_visible -> (mc_credits_y>-window_height and mc_credits_y<window_height)
// ----------------------------------------------------------------------------
//   Tells if the current credit is visible
// ----------------------------------------------------------------------------


mc_credit Role:text, Name:text ->
// ----------------------------------------------------------------------------
//   Display a single credit
// ----------------------------------------------------------------------------
    if mc_credits_visible then
        mcx -> mc_credits_width * 0.5
        mcw -> mc_credits_width * 0.95
        text_box -mcx, 0, mcw, mc_credits_height,
            align 1
            text Role
        text_box mcx, 0, mcw, mc_credits_height,
            align 0
            text Name
    mc_credits_skip mc_credits_height


mc_credits_section H:real, Body ->
// ----------------------------------------------------------------------------
//   Add a centered section
// ----------------------------------------------------------------------------
    if mc_credits_visible then
        text_box 0, -H/2, mc_credits_width, H,
            align 0.5
            vertical_align 0.5
            Body
    mc_credits_skip H



// ============================================================================
//   Settings
// ============================================================================

// Width and height of an item
mc_credits_height -> 50.0
mc_credits_height H:real -> mc_credits_height := H
mc_credits_width -> 900.0
mc_credits_width W:real -> mc_credits_width := W

mc_credits_y -> 0.0



// ****************************************************************************
// 
//   Internal helpers
// 
// ****************************************************************************

mc_swc_background ->
// ----------------------------------------------------------------------------
//   Show the stars in the background
// ----------------------------------------------------------------------------
    locally 
        // We map the stars background on a large sphere in the background
        color "white"
        texture mc_swc_background_texture
        translate 0, 0, -10000
        rotatey 0.03 * time + 90 

        // We invert the sphere because we are inside it
        scale 1, -1, 1 

        // Wrap the texture and repeat it 8 times in each direction
        texture_wrap true, true 
        texture_transform 
            scale 12, 12, 12
        sphere 0, 0, 0, 32000 


mc_swc_planet Delay:real, Texture:text ->
// ----------------------------------------------------------------------------
//   Draw the planet with the given texture
// ----------------------------------------------------------------------------

    locally
        color "white"
        texture mc_swc_planet_texture
        texture_wrap true, true
        texture_transform
            scale 4, 4, 4
        translate 0,-2900-100 * expm1(Delay - page_time),-200
        rotatex -60
        rotatez 27
        rotatey 0.03 * time
        sphere 0, 0, 0, 5000,5000,5000,100,100

        texture ""
        color 70%, 80%, 100%, 10%
        sphere 0, 0, 0, 5010,5010,5010,100,100

        texture mc_swc_clouds_texture
        color "white"
        texture_wrap true, true
        texture_transform
            scale 2, 1, 1
        sphere 0, 0, 0, 5020,5020,5020,100,100


mc_swc_text Duration:real, Body ->
// ----------------------------------------------------------------------------
//   Draw the Star Wars scrolling text
// ----------------------------------------------------------------------------
    locally 
        vertical_align 0.0 
        align 0.5, 1.0
        color 1, 0.8, 0.2, 0.9+0.9*expm1 (Duration-page_time)
        line_color 1, 0.8, 0.2, 0.9+0.9*expm1 (Duration-page_time)
        line_width 0.05
        font "Arial Narrow", 64, bold 
        translate 0, -384, 0
        rotatex -80
        translatey -3000 + 6000 * page_time / Duration
        scale 1, 3, 1
        paragraph_space 50, 50
        text_box 0, 0, 1200, 2000, Body


// Add this module's images/ directory to the image search path
add_search_path "image:", module_dir & "/images"
